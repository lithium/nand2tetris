
/*
 * Resizable array
 */

class ArrayList
{
  field Array _array;
  field int _capacity;
  field int _size;

  constructor ArrayList new(int capacity)
  {
    let _array = Array.new(capacity);
    let _size = 0;
    let _capacity = capacity;
    return this;
  }

  method void dispose()
  {
    do _array.dispose();
    let _array = null;
    let _size = 0;
    let _capacity = 0;
    return;
  }

  method void add(Void obj)
  {
    if ((_size+1 > _capacity) | (_size+1 = _capacity)) {
      do resize(_capacity*2);
    }

    let _array[_size] = obj;
    let _size = _size + 1;
    return;
  }

  method Void get(int index) {
    return _array[index];
  }

  method int size() {
    return _size;
  }

  method void resize(int newCapacity)
  {
    var Array arr;
    var int i;

    let arr = Array.new(newCapacity);
    let i = 0;
    while (i < _size) {
      let arr[i] = _array[i];
      let i = i + 1;
    }
    do _array.dispose();
    let _array = arr;
    let _capacity = newCapacity;

    return;
  }

  method int indexOf(String str)
  {
    var int i;
    var String s;

    let i = 0;
    while (i < _size) {
      let s = _array[i];
      if (s.equals(str)) {
        return i;
      }
      let i = i + 1;
    }

    return -1;
  }
}